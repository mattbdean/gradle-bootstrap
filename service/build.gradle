buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'net.swisstech:gradle-dropwizard:1.1.11'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

ext {
    dropwizardVersion = '0.9.1'
    resourcesFolder = 'src/main/resources/'
    assetsFolder = resourcesFolder + 'assets/'
}

apply plugin: "kotlin"
apply plugin: "net.swisstech.dropwizard"
apply plugin: "eu.appsatori.fatjar"

repositories {
    mavenCentral()
}

fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': 'net.dean.gbs.web.ApplicationKt'
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

dependencies {
    for (module in ['assets', 'core', 'db', 'jackson', 'logging', 'hibernate']) {
        compile "io.dropwizard:dropwizard-$module:$dropwizardVersion"
    }

    runtime "com.h2database:h2:1.4.190"
    // Would use 5.0.0.GA but something Hibernate-related causes tests to fail
    compile "org.jadira.usertype:usertype.core:4.0.0.GA"
    compile project(':api')
    testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
}

clean {
    // Assets will be copied again by copyWebsite
    delete 'src/main/resources/assets'
}

dropwizard {
    mainClass = 'net.dean.gbs.web.ApplicationKt'
    dropwizardConfigFile = 'config/local.yml'
}

// Copy website files into :service's resources
task copyWebsite(type: Copy, dependsOn: [':website:build']) {
    from '../website/build'
    into 'src/main/resources/assets'
}

// Task called by Heroku to build a fully working Jar
task stage(type: Copy, dependsOn: ['clean', 'fatJar', 'copyWebsite'])

// Clean, get the latest version of the website, and run the service
task run(dependsOn: ['clean', 'copyWebsite', 'dropwizardRun'])

defaultTasks 'run'
