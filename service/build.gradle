buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'net.swisstech:gradle-dropwizard:+'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

ext {
    dropwizardVersion = '0.8.0-rc2'
    resourcesFolder = 'src/main/resources/'
    assetsFolder = resourcesFolder + 'assets/'
}

apply plugin: "kotlin"
apply plugin: "net.swisstech.dropwizard"
apply plugin: "eu.appsatori.fatjar"

repositories {
    mavenCentral()
}

fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': 'net.dean.gbs.web.GradleBootstrap'
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

dependencies {
    for (module in ['assets', 'core', 'db', 'jackson', 'logging', 'hibernate', 'views-freemarker']) {
        compile "io.dropwizard:dropwizard-$module:$dropwizardVersion"
    }

    runtime "com.h2database:h2:1.4.184"
    compile "org.jadira.usertype:usertype.core:3.2.0.GA"
    compile project(':api')
    testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
}

dropwizard {
    mainClass = 'net.dean.gbs.web.GradleBootstrap'
    dropwizardConfigFile = 'config/embedded.yml'
}

task copyJs(type: Copy, dependsOn: [':website:build']) {
    from '../website/build/css'
    into assetsFolder + 'css'
}

task copyCss(type: Copy, dependsOn: [':website:build']) {
    from '../website/build/js'
    into assetsFolder + 'js'
}

task copyViews(type: Copy, dependsOn: [':website:build']) {
    from('../website/build') {
        include '*.ftl'
    }
    into resourcesFolder + 'net/dean/gbs/web/views'
}

[copyJs, copyCss, copyViews].each {
    it.mustRunAfter ':website:build'
}

task stage(type: Copy, dependsOn: ['clean', 'fatJar', ':website:build', 'copyJs', 'copyCss', 'copyViews']) {}

